# check if exa exists otherwise use ls
if command -v exa > /dev/null
    abbr -a ls 'exa'
    abbr -a ll 'exa -laF'
else
    abbr -a l 'ls'
    abbr -a ll 'ls -laF'
end

set -x FZF_DEFAULT_COMMAND 'fd --type f --hidden --follow --exclude ".git"'
set -x TWITCH_CLIENT_ID 'q1lh0xkjtys6k1ca978ixs39cy5b1a'
set -x TWITCH_CLIENT_SECRET 'itl4xqwkgr0h4mnbx8o8eq65n8thlc'
set -x TWITCH_BOT_TOKEN 'oauth:b35yb11grtmmfgwqs3by2i57w5z0qb'

abbr -a vim 'nvim'
abbr -a cb 'cargo build'
abbr -a cr 'cargo run'
abbr -a ct 'cargo test'
abbr -a ef 'nvim ~/.config/fish/config.fish'
abbr -a ev 'nvim ~/.config/nvim/init.vim'
abbr -a cat 'bat'

# display full directory names in prompt
set fish_prompt_pwd_dir_length 0
set __fish_git_prompt_showdirtystate 'yes'
set __fish_git_prompt_showuntrackedfiles 'yes'
set __fish_git_prompt_showstashstate ''
set __fish_git_prompt_showupstream 'none'

set -x PATH $PATH /snap/bin
set -x PATH $PATH /home/thomas/bin
set -x PATH $PATH ~/.cargo/bin

function fish_prompt
	set_color brblack
	echo -n "["(date "+%H:%M")"] "
	set_color blue
	echo -n (hostname)
	if [ $PWD != $HOME ]
		set_color brblack
		echo -n ':'
		set_color yellow
		echo -n (basename $PWD)
	end
	set_color green
	printf '%s ' (__fish_git_prompt)
	set_color red
	echo -n '| '
	set_color normal
end

function ignorehistory --on-event fish_prompt
    echo "all" | history delete --prefix ll ls exit echo clear cd > /dev/null
end

function fish_greeting
	echo
	echo -e (uname -ro | awk '{print " \\\\e[1mOS: \\\\e[0;32m"$0"\\\\e[0m"}')
	echo -e (uptime -p | sed 's/^up //' | awk '{print " \\\\e[1mUptime: \\\\e[0;32m"$0"\\\\e[0m"}')
	echo -e (uname -n | awk '{print " \\\\e[1mHostname: \\\\e[0;32m"$0"\\\\e[0m"}')
	echo -e " \\e[1mDisk usage:\\e[0m"
	echo
	echo -ne (\
		df -l -h | grep -E 'dev/(xvda|sd|mapper)' | \
		awk '{printf "\\\\t%s\\\\t%4s / %4s  %s\\\\n\n", $6, $3, $2, $5}' | \
		sed -e 's/^\(.*\([8][5-9]\|[9][0-9]\)%.*\)$/\\\\e[0;31m\1\\\\e[0m/' -e 's/^\(.*\([7][5-9]\|[8][0-4]\)%.*\)$/\\\\e[0;33m\1\\\\e[0m/' | \
		paste -sd ''\
	)
	echo

	echo -e " \\e[1mNetwork:\\e[0m"
	echo
	# http://tdt.rocks/linux_network_interface_naming.html
	echo -ne (\
		ip addr show up scope global | \
			grep -E ': <|inet' | \
			sed \
				-e 's/^[[:digit:]]\+: //' \
				-e 's/: <.*//' \
				-e 's/.*inet[[:digit:]]* //' \
				-e 's/\/.*//'| \
			awk 'BEGIN {i=""} /\.|:/ {print i" "$0"\\\n"; next} // {i = $0}' | \
			sort | \
			column -t | \
			# public addresses are underlined for visibility \
			sed 's/ \([^ ]\+\)$/ \\\e[4m\1/' | \
			# private addresses are not \
			sed 's/m\(\(10\.\|172\.\(1[6-9]\|2[0-9]\|3[01]\)\|192\.168\.\).*\)/m\\\e[24m\1/' | \
			# unknown interfaces are cyan \
			sed 's/^\( *[^ ]\+\)/\\\e[36m\1/' | \
			# ethernet interfaces are normal \
			sed 's/\(\(en\|em\|eth\)[^ ]* .*\)/\\\e[39m\1/' | \
			# wireless interfaces are purple \
			sed 's/\(wl[^ ]* .*\)/\\\e[35m\1/' | \
			# wwan interfaces are yellow \
			sed 's/\(ww[^ ]* .*\).*/\\\e[33m\1/' | \
			sed 's/$/\\\e[0m/' | \
			sed 's/^/\t/' \
		)
	echo

	set r (random 0 100)
	if [ $r -lt 5 ] # only occasionally show backlog (5%)
		echo -e " \e[1mBacklog\e[0;32m"
		set_color blue
		echo "  [project] <description>"
		echo
	end

	set_color normal
	echo -e " \e[1mTODOs\e[0;32m"
	echo
	if [ $r -lt 10 ]
		# unimportant, so show rarely
		set_color cyan
		# echo "  [project] <description>"
	end
	if [ $r -lt 25 ]
		# back-of-my-mind, so show occasionally
		set_color green
		echo "  [Change wallpaper] fix so wallpaper goes with theme"
		echo "  [Enigma machine] Finish coding the enigma machine"
		echo "  [VHDX parser] Start writing a vhdx parser"
	end
	if [ $r -lt 50 ]
		# upcoming, so prompt regularly
		set_color yellow
		echo "  [Color bottom menu] change the color of the bottom menu"
	end

	# urgent, so prompt always
	set_color red
	# echo "  [project] <description>"

	echo

	if test -s ~/todo
		set_color magenta
		cat todo | sed 's/^/ /'
		echo
	end

	set_color normal
end

# sourcing start scripts in /bin/autoload if folder exists
if test -d ~/bin/autoload
  for file in ~/bin/autoload/*.fish
    if test -f $file
      . $file
    end
  end
end
